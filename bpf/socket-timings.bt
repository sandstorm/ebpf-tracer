#!/usr/local/bin/bpftrace

// Measure Socket Timings for stuff like DB connections.
// This works for interlocked request/responses, so for stuff like MariaDB connections.

// TODO: measure on sys_exit_recvfrom??

BEGIN {
        // printf("Monitoring processes of cgroup %d", cgroupid(str($1)));
}

END {
    clear(@start);
    clear(@end);
    printf("\n\nLatency Histogram from the start of the first request to the start of the first reply.\nIndexed by PID, FD\nin us");
}


tracepoint:syscalls:sys_enter_sendto /pid >= $1 && pid <= $2/
{
    if (@end[pid, tid, args->fd] == 0) {
        // first connection
        if (@start[pid, tid, args->fd] == 0) {
            @start[pid, tid, args->fd] = nsecs;
        }
    } else {
        // old connection finished, starting a new one
        $duration_us = (@end[pid, tid, args->fd] - @start[pid, tid, args->fd]) / 1000; // in us
        @us[pid, args->fd] = lhist($duration_us, 0, 5000, 100);

        // new start
        @start[pid, tid, args->fd] = nsecs;
        delete(@end[pid, tid, args->fd]);
    }

}

// TODO: measure on sys_exit_recvfrom??
tracepoint:syscalls:sys_enter_recvfrom /pid >= $1 && pid <= $2/
{
    @end[pid, tid, args->fd] = nsecs;
}
